plugins {
    id 'java'
    id 'io.quarkus' version '3.17.0'
    id 'jacoco'
}

group = 'scrabble.phrases'
version = '2.0.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation enforcedPlatform('io.quarkus.platform:quarkus-bom:3.17.0')
    implementation 'io.quarkus:quarkus-rest'
    implementation 'io.quarkus:quarkus-rest-jackson'
    implementation 'io.quarkus:quarkus-smallrye-health'
    implementation 'io.quarkus:quarkus-arc'

    testImplementation 'io.quarkus:quarkus-junit5'
    testImplementation 'io.rest-assured:rest-assured'
    testImplementation 'org.assertj:assertj-core:3.26.3'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.compilerArgs.add('-parameters')
}

test {
    systemProperty 'java.util.logging.manager', 'org.jboss.logmanager.LogManager'
    systemProperty 'file.encoding', 'UTF-8'
    useJUnitPlatform()
}

jacoco {
    toolVersion = '0.8.12'
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = false
        csv.required = false
        html.required = true
        html.outputLocation = layout.buildDirectory.dir('reports/jacoco')
    }
}

// Download Romanian Scrabble dictionary during resource processing
tasks.register('downloadDictionary') {
    def wordsFile = file('src/main/resources/words.txt')
    outputs.file(wordsFile)

    doLast {
        if (!wordsFile.exists()) {
            def zipFile = file("${layout.buildDirectory.get()}/scrabble.zip")
            ant.mkdir(dir: layout.buildDirectory.get())
            ant.get(src: 'https://dexonline.ro/static/download/scrabble/loc-baza-5.0.zip', dest: zipFile)

            // Verify SHA-256 checksum of the downloaded file
            def digest = java.security.MessageDigest.getInstance('SHA-256')
            zipFile.withInputStream { is ->
                byte[] buf = new byte[8192]
                int len
                while ((len = is.read(buf)) != -1) {
                    digest.update(buf, 0, len)
                }
            }
            def actualHash = digest.digest().collect { String.format('%02x', it) }.join()
            // To update: delete src/main/resources/words.txt, run build, and use the actual hash from the error message
            def expectedHash = '90b05117d397c845e0e65478a5084ed29a82877d7dd0099e91533434ae3ed95d'
            if (actualHash != expectedHash) {
                ant.delete(file: zipFile)
                throw new GradleException(
                    "Dictionary checksum mismatch!\nExpected: ${expectedHash}\nActual:   ${actualHash}\n" +
                    "If the dictionary was intentionally updated, update the expected hash in build.gradle."
                )
            }

            ant.unzip(src: zipFile, dest: 'src/main/resources/')
            ant.delete(file: zipFile)
            ant.move(file: 'src/main/resources/loc-baza-5.0.txt', tofile: wordsFile)
        }
    }
}

processResources.dependsOn downloadDictionary
