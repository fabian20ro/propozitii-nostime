plugins {
    id 'java'
    id 'io.quarkus' version '3.17.0'
    id 'jacoco'
    id 'org.jetbrains.kotlin.jvm' version '2.0.21'
    id 'org.jetbrains.kotlin.plugin.allopen' version '2.0.21'
}

group = 'scrabble.phrases'
version = '3.0.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

allOpen {
    annotation("jakarta.ws.rs.Path")
    annotation("jakarta.enterprise.context.ApplicationScoped")
}

repositories {
    mavenCentral()
}

dependencies {
    implementation enforcedPlatform('io.quarkus.platform:quarkus-bom:3.17.0')
    implementation 'io.quarkus:quarkus-rest'
    implementation 'io.quarkus:quarkus-rest-jackson'
    implementation 'io.quarkus:quarkus-smallrye-health'
    implementation 'io.quarkus:quarkus-arc'
    implementation 'io.quarkus:quarkus-kotlin'
    implementation 'io.quarkus:quarkus-jdbc-postgresql'
    implementation 'io.quarkus:quarkus-agroal'
    implementation 'io.quarkus:quarkus-flyway'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib'

    testImplementation 'io.quarkus:quarkus-junit5'
    testImplementation 'io.rest-assured:rest-assured'
    testImplementation 'org.assertj:assertj-core:3.26.3'
    testImplementation 'org.testcontainers:testcontainers'
    testImplementation 'org.testcontainers:postgresql'
    testImplementation 'org.testcontainers:junit-jupiter'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.compilerArgs.add('-parameters')
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    compilerOptions {
        jvmTarget = org.jetbrains.kotlin.gradle.dsl.JvmTarget.JVM_21
        javaParameters = true
    }
}

def npmCommand = System.getProperty("os.name").toLowerCase().contains("win") ? "npm.cmd" : "npm"
def hasPackageLock = file("package-lock.json").exists()

tasks.register("npmInstall", Exec) {
    workingDir = projectDir
    commandLine npmCommand, hasPackageLock ? "ci" : "install"
    inputs.file("package.json")
    if (hasPackageLock) {
        inputs.file("package-lock.json")
    }
    outputs.dir("node_modules")
}

tasks.register("testVercelFallback", Exec) {
    dependsOn "npmInstall"
    workingDir = projectDir
    commandLine npmCommand, "test"
    inputs.file("package.json")
    if (hasPackageLock) {
        inputs.file("package-lock.json")
    }
    inputs.files(fileTree("api") { include("**/*.ts") })
    // Always execute like JVM tests to avoid stale pass caching.
    outputs.upToDateWhen { false }
}

test {
    dependsOn "testVercelFallback"
    systemProperty 'java.util.logging.manager', 'org.jboss.logmanager.LogManager'
    systemProperty 'file.encoding', 'UTF-8'
    useJUnitPlatform()
    testLogging {
        events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        exceptionFormat 'full'
        showCauses true
        showStackTraces true
    }
}

jacoco {
    toolVersion = '0.8.12'
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = false
        csv.required = false
        html.required = true
        html.outputLocation = layout.buildDirectory.dir('reports/jacoco')
    }
}

// Dictionary download task - only needed for the data loader tool, not for runtime
tasks.register('downloadDictionary') {
    def wordsFile = file('src/main/resources/words.txt')
    outputs.file(wordsFile)

    doLast {
        if (!wordsFile.exists()) {
            def zipFile = file("${layout.buildDirectory.get()}/scrabble.zip")
            ant.mkdir(dir: layout.buildDirectory.get())
            ant.get(src: 'https://dexonline.ro/static/download/scrabble/loc-baza-5.0.zip', dest: zipFile)

            // Verify SHA-256 checksum of the downloaded file
            def digest = java.security.MessageDigest.getInstance('SHA-256')
            zipFile.withInputStream { is ->
                byte[] buf = new byte[8192]
                int len
                while ((len = is.read(buf)) != -1) {
                    digest.update(buf, 0, len)
                }
            }
            def actualHash = digest.digest().collect { String.format('%02x', it) }.join()
            // To update: delete src/main/resources/words.txt, run build, and use the actual hash from the error message
            def expectedHash = '2bbf9529ed7e1cf1b489e94d16aa82436e568e59066e6c53f669eeb855e3a136'
            if (actualHash != expectedHash) {
                ant.delete(file: zipFile)
                throw new GradleException(
                    "Dictionary checksum mismatch!\nExpected: ${expectedHash}\nActual:   ${actualHash}\n" +
                    "If the dictionary was intentionally updated, update the expected hash in build.gradle."
                )
            }

            ant.unzip(src: zipFile, dest: 'src/main/resources/')
            ant.delete(file: zipFile)
            ant.move(file: 'src/main/resources/loc-baza-5.0.txt', tofile: wordsFile)
        }
    }
}

// Data loader task - loads dictionary into Supabase PostgreSQL
tasks.register('loadDictionary', JavaExec) {
    dependsOn 'classes'
    mainClass = 'scrabble.phrases.tools.LoadDictionaryKt'
    classpath = sourceSets.main.runtimeClasspath
    systemProperty 'file.encoding', 'UTF-8'
}

tasks.register('rarityStep1Export', JavaExec) {
    dependsOn 'classes'
    mainClass = 'scrabble.phrases.tools.RarityPipelineKt'
    classpath = sourceSets.main.runtimeClasspath
    systemProperty 'file.encoding', 'UTF-8'
    systemProperty 'rarity.step', 'step1'
}

tasks.register('rarityStep2Score', JavaExec) {
    dependsOn 'classes'
    mainClass = 'scrabble.phrases.tools.RarityPipelineKt'
    classpath = sourceSets.main.runtimeClasspath
    systemProperty 'file.encoding', 'UTF-8'
    systemProperty 'rarity.step', 'step2'
}

tasks.register('rarityStep3Compare', JavaExec) {
    dependsOn 'classes'
    mainClass = 'scrabble.phrases.tools.RarityPipelineKt'
    classpath = sourceSets.main.runtimeClasspath
    systemProperty 'file.encoding', 'UTF-8'
    systemProperty 'rarity.step', 'step3'
}

tasks.register('rarityStep4Upload', JavaExec) {
    dependsOn 'classes'
    mainClass = 'scrabble.phrases.tools.RarityPipelineKt'
    classpath = sourceSets.main.runtimeClasspath
    systemProperty 'file.encoding', 'UTF-8'
    systemProperty 'rarity.step', 'step4'
}

tasks.register('rarityStep5Rebalance', JavaExec) {
    dependsOn 'classes'
    mainClass = 'scrabble.phrases.tools.RarityPipelineKt'
    classpath = sourceSets.main.runtimeClasspath
    systemProperty 'file.encoding', 'UTF-8'
    systemProperty 'rarity.step', 'step5'
}
